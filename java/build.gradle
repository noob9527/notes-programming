buildscript {
    ext {
        kotlinVersion = '1.2.0'

        junitPlatformVersion = '1.0.2'
        junitJupiterVersion = '5.0.2'
        mockitoInlineVersion ='2.27.0'

        javaMailVersion = '1.6.2'

        gravaVersion = '21.0'

        grpcVersion = '1.18.0' // CURRENT_GRPC_VERSION
        protobufVersion = '3.6.1'
        protocVersion = protobufVersion
    }

    ext.repos = {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        jcenter()
        mavenCentral()
    }
    
    repositories repos

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath("org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion")
    }
}

plugins {
    id 'com.google.protobuf' version '0.8.5'
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'org.junit.platform.gradle.plugin'

group 'cn.staynoob'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories repos

allprojects {
    repositories repos
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    compile("com.sun.mail:javax.mail:${javaMailVersion}")

    compile ("com.google.guava:guava:${gravaVersion}")

    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"

    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    // To avoid compiler warnings about @API annotations in JUnit code
    testCompileOnly('org.apiguardian:apiguardian-api:1.0.0')
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile "org.mockito:mockito-inline:${mockitoInlineVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                // By default generated Java files are
                // under $generatedFilesBaseDir/$sourceSet/$builtinPluginName,
                // where $generatedFilesBaseDir is $buildDir/generated/source/proto by default
                // see https://github.com/google/protobuf-gradle-plugin#change-where-files-are-generated
                outputSubDir = 'java'
            }
        }
    }
}
